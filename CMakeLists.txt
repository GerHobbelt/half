# Copyright (C) 2015 Ruslan Baratov, David Hirvonen
# License: Boost Software License
# See LICENSE.txt for the full license.

cmake_minimum_required(VERSION 3.0)
project(half VERSION 1.1.0)

add_library(half INTERFACE)

if(ANDROID)
  set(default_value OFF)
else()
  set(default_value ON)
endif()

option(HALF_ENABLE_CPP11_CMATH "Enable c++11 cmath" "${default_value}")

if(HALF_ENABLE_CPP11_CMATH)
  set(define_value 1)
else()
  set(define_value 0)
endif()

target_compile_definitions(half INTERFACE HALF_ENABLE_CPP11_CMATH=${define_value})

### Install ###
set(generated_dir "${CMAKE_CURRENT_BINARY_DIR}/generated")

set(config_install_dir "lib/cmake/${PROJECT_NAME}")
set(include_install_dir "include")

set(version_config "${generated_dir}/${PROJECT_NAME}ConfigVersion.cmake")
set(project_config "${generated_dir}/${PROJECT_NAME}Config.cmake")
set(targets_export_name "${PROJECT_NAME}Targets")
set(namespace "${PROJECT_NAME}::")

include(CMakePackageConfigHelpers)
write_basic_package_version_file(
    "${version_config}" COMPATIBILITY SameMajorVersion
)

# Note: use 'targets_export_name'
configure_file("cmake/Config.cmake.in" "${project_config}" @ONLY)

install(
    TARGETS half
    EXPORT "${targets_export_name}"
    INCLUDES DESTINATION "${include_install_dir}"
)

install(
    DIRECTORY "half"
    DESTINATION "${include_install_dir}"
)

install(
    FILES "${project_config}" "${version_config}"
    DESTINATION "${config_install_dir}"
)

install(
    EXPORT "${targets_export_name}"
    NAMESPACE "${namespace}"
    DESTINATION "${config_install_dir}"
)
